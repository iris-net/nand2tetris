// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
        Inc16(in=regOut, out=incTmp);
        Mux16(a=regOut, b=incTmp, sel=inc, out=incMux);
        Mux16(a=incMux, b=in, sel=load, out=loadMux);
        Mux16(a=loadMux, b=false, sel=reset, out=resetMux);

        Or(a=reset, b=load, out=resetOrLoad);
        Or(a=resetOrLoad, b=inc, out=resetOrLoadOrInc);

        Register(in=resetMux, load=resetOrLoadOrInc, out=regOut);

        Or16(a=regOut, b=false, out=out);
}
